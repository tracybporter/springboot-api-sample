import com.wiredforcode.gradle.spawn.*

buildscript {
   repositories {
      mavenCentral()
      maven { url 'http://dl.bintray.com/vermeulen-mp/gradle-plugins' }
   }
   dependencies {
      classpath 'com.wiredforcode:gradle-spawn-plugin:0.6.0'
   }
}

dependencies {
   testCompile "org.springframework.boot:spring-boot-starter-test"
   testCompile "org.springframework:spring-test"
   testCompile "org.spockframework:spock-core"
   testCompile 'org.spockframework:spock-spring:1.0-groovy-2.4'
   testCompile "cglib:cglib:2.2"
}

test {
   systemProperties System.properties
}

sourceSets {
   acceptance {
      groovy.srcDir file('src/acceptance/groovy')
      resources.srcDir file('src/acceptance/resources')
      compileClasspath += sourceSets.test.compileClasspath
      runtimeClasspath += sourceSets.test.runtimeClasspath
   }
}

task runAcceptance(type: Test, dependsOn: ['startMockServer']) {
   description = 'Runs the acceptance tests'
   systemProperties System.properties
   group = 'verification'
   testClassesDir = sourceSets.acceptance.output.classesDir
   classpath = sourceSets.acceptance.runtimeClasspath
   reports.junitXml.destination = 'build/acceptance-results'
   reports.html.destination = 'build/reports/acceptance'
}

test.finalizedBy runAcceptance

task startMockServer(type: SpawnProcessTask, dependsOn: 'assemble') {
   description = 'Starting the external mock server'
   command "java -jar src/acceptance/resources/stubby4j-3.3.0.jar -d src/acceptance/resources/sample-mock-server.yml"
   ready 'Jetty successfully started'
}

task stopMockServer(type: KillProcessTask)

runAcceptance.finalizedBy stopMockServer